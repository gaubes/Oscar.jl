export AbsMultSet, AbsComplementOfPrimeIdeal
export ComplementOfPrimeIdeal


export AbsLocalizedRing
export original_ring, inverted_set
export LocalizedRing

export AbsLocalizedRingElem
export fraction, parent

export AbsLocalRing
export MPolyLocalRing

import AbstractAlgebra.Ring

#################################################################################
# General framework for localizations of rings; to be used with affine algebras #
#################################################################################

#################################################################################
# Multiplicatively closed sets of (commutative) rings                           #
#################################################################################
@Markdown.doc """
    AbsMultSet{RingType, RingElemType}

Abstract type for a multiplicatively closed set in a commutative (Noetherian) ring 
R of type `RingType` with elements of type `RingElemType`.
"""
abstract type AbsMultSet{RingType, RingElemType} end

@Markdown.doc """
    in(f::RingElemType, S::AbsMultSet{RingType, RingElemType}) where {RingType, RingElemType}

Returns `true` if `f` belongs to `S`; `false` otherwise.

**Note:** If this routine is not implemented, the function call will default to the 
execution of an error message. 
"""
function Base.in(f::RingElemType, S::AbsMultSet{RingType, RingElemType}) where {RingType, RingElemType}
  error("method not implemented for multiplicatively closed sets of type $(typeof(S))")
end

#################################################################################
# Localizations of (commutative) rings at multiplicatively closed sets          #
#################################################################################
@Markdown.doc """
    AbsLocalizedRing{RingType, RingElemType, MultSetType}

The localization R[S‚Åª¬π] of a ring R of type `RingType` with elements of type `RingElemType` at a 
multiplicatively closed set S of type `MultSetType`. 

In general, the arithmetic of such a localized ring R[S‚Åª¬π] should be implemented using fractions 
of elements in the original ring R. The methods provided for the multiplicatively closed set S 
can be used to check whether a given denominator is admissible for the specific localization. 

Depending on the actual type of R and S, further functionality can then be provided using 
different Groebner-basis driven backends. 
"""
abstract type AbsLocalizedRing{RingType, RingElemType, MultSetType} <: Ring end

@Markdown.doc """
    original_ring(W::AbsLocalizedRing{RingType, RingElemType, MultSetType}) where {RingType, RingElemType, MultSetType} 

Returns the original ring R for a localized ring of the form W = R[S‚Åª¬π].
"""
function original_ring(W::AbsLocalizedRing{RingType, RingElemType, MultSetType}) where {RingType, RingElemType, MultSetType} 
  error("`original_ring` is not implemented for localized rings of type $(typeof(W))")
end

@Markdown.doc """
    inverted_set(W::AbsLocalizedRing{RingType, RingElemType, MultSetType}) where {RingType, RingElemType, MultSetType}

Returns the set S of at which has been localized for a localized ring W = R[S‚Åª¬π].
"""
function inverted_set(W::AbsLocalizedRing{RingType, RingElemType, MultSetType}) where {RingType, RingElemType, MultSetType}
  error("`inverted_set` is not implemented for localized rings of type $(typeof(W))")
end

@Markdown.doc """
    (W::AbsLocalizedRing{RingType, RingElemType, MultSetType})(f::AbstractAlgebra.Generic.Frac{RingElemType}) where {RingType, RingElemType, MultSetType} 

Converts a fraction f = a//b to an element of the localized ring W.
"""
function (W::AbsLocalizedRing{RingType, RingElemType, MultSetType})(f::AbstractAlgebra.Generic.Frac{RingElemType}) where {RingType, RingElemType, MultSetType} 
  error("conversion for fractions to elements of type $(typeof(W)) is not implemented")
end

#################################################################################
# A minimal implementation of the above abstract interface                      #
#################################################################################
@Markdown.doc """
    LocalizedRing{RingType, RingElemType, MultSetType} <: AbsLocalizedRing{RingType, RingElemType, MultSetType} 

A minimal implementation for the localization R[S‚Åª¬π] of a ring R of type `RingType` with 
elements of type `RingElemType`, localized at a multiplicatively closed set S of 
type `MultSetType`.
"""
mutable struct LocalizedRing{RingType, RingElemType, MultSetType} <: AbsLocalizedRing{RingType, RingElemType, MultSetType} 
  R::RingType # The parent ring which is being localized
  S::MultSetType # The multiplicatively closed set S ‚äÇ R whose inverses are added to R

  function LocalizedRing(R::RingType, S::MultSetType) where {RingType, MultSetType}
    # Sanity check whether the multiplicatively closed set S is compatible with the 
    # given rings
    MultSetType <: AbsMultSet{RingType, elem_type(RingType)} || error(
	"The type of the multiplicatively closed set is not compatible with the type of the ring"
	)
    R_loc = new{RingType, elem_type(RingType), MultSetType}(R,S)
    return R_loc
  end
end

original_ring(W::LocalizedRing{RingType, RingElemType, MultSetType}) where {RingType, RingElemType, MultSetType} = W.R

inverted_set(W::LocalizedRing{RingType, RingElemType, MultSetType}) where {RingType, RingElemType, MultSetType} = W.S

#################################################################################
# Elements of localized rings                                                   #
#################################################################################
@Markdown.doc """
    AbsLocalizedRingElem{RingType, RingElemType, MultSetType}

The abstract type of an element of the localization R[S‚Åª¬π] of a commutative ring 
R of type `RingType` with elements of type `RingElemType` at a multiplicatively 
closed set S of type `MultSetType`.
"""
abstract type AbsLocalizedRingElem{RingType, RingElemType, MultSetType} end

@Markdown.doc """
    fraction(f::T) where {T<:AbsLocalizedRingElem} 

Returns the actual value of `f` as a fraction in the localization of the original ring.
"""
function fraction(f::T) where {T<:AbsLocalizedRingElem} 
  error("`fraction` is not implemented for the type $(typeof(f))")
end

@Markdown.doc """
    parent(f::T) where {T<:AbsLocalizedRingElem}

Returns the parent ring R[S‚Åª¬π] of `f`.
"""
function parent(f::T) where {T<:AbsLocalizedRingElem}
  error("`parent` is not implemented for the type $(typeof(f))")
end

function +(a::AbsLocalizedRingElem{RingType, RingElemType, MultSetType}, b::AbsLocalizedRingElem{RingType, RingElemType, MultSetType}) where {RingType, RingElemType, MultSetType}
  parent(a) == parent(b) || error("the arguments do not have the same parent ring")
  return (parent(a))(fraction(a) + fraction(b))
end

function -(a::AbsLocalizedRingElem{RingType, RingElemType, MultSetType}, b::AbsLocalizedRingElem{RingType, RingElemType, MultSetType}) where {RingType, RingElemType, MultSetType}
  parent(a) == parent(b) || error("the arguments do not have the same parent ring")
  return (parent(a))(fraction(a) - fraction(b))
end

function *(a::AbsLocalizedRingElem{RingType, RingElemType, MultSetType}, b::AbsLocalizedRingElem{RingType, RingElemType, MultSetType}) where {RingType, RingElemType, MultSetType}
  parent(a) == parent(b) || error("the arguments do not have the same parent ring")
  return (parent(a))(fraction(a) * fraction(b))
end

function Base.:(//)(a::AbsLocalizedRingElem{RingType, RingElemType, MultSetType}, b::AbsLocalizedRingElem{RingType, RingElemType, MultSetType}) where {RingType, RingElemType, MultSetType}
  parent(a) == parent(b) || error("the arguments do not have the same parent ring")
  numerator(fraction(b)) in inverted_set(parent(b)) || error("the second argument is not a unit in this local ring")
  return (parent(a))(fraction(a) // fraction(b))
end

function ==(a::T, b::T) where {T<:AbsLocalizedRingElem}
  parent(a) == parent(b) || error("the arguments do not have the same parent ring")
  return parent(a)(fraction(a) == fraction(b))
end

function ^(a::T, i::Int) where {T<:AbsLocalizedRingElem}
  parent(a) == parent(b) || error("the arguments do not have the same parent ring")
  return parent(a)(fraction(a)^i)
end

#################################################################################
# A minimal implementation of elements of localized rings                       #
#################################################################################
@Markdown.doc """
    mutable struct LocalizedRingElem{RingType, RingElemType, MultSetType}

A minimal implementation for elements f in a localized ring R[S‚Åª¬π] where 
R is of type `RingType`, with elements of type `RingElemType`, and 
S of type `MultSetType`. 
"""
mutable struct LocalizedRingElem{RingType, RingElemType, MultSetType}
  frac::AbstractAlgebra.Generic.Frac{RingElemType} # The element of the localized ring as a fraction with admissible denominator
  R_loc::LocalizedRing{RingType, RingElemType, MultSetType} # the parent ring before localization 

  function LocalizedRingElem(f::AbstractAlgebra.Generic.Frac{RingElemType}, R_loc::LocalizedRing{RingType, RingElemType, MultSetType}) where {RingType, RingElemType, MultSetType}
    # Perform some sanity checks
    RingType == parent_type(RingElemType) || error(
	"The given type of ring is incompatible with the given element type"
	)
    MultSetType <: AbsMultSet{RingType, RingElemType} || error(
	"The given type of the multiplicatively closed set is incompatible with the given ring types"
	)
    return new{RingType, RingElemType, MultSetType}(f, R_loc)
  end
end

# The required interface
function parent(f::LocalizedRingElem{RingType, RingElemType, MultSetType}) where {RingType, RingElemType, MultSetType} 
  return f.R_loc
end

function frac(f::LocalizedRingElem{RingType, RingElemType, MultSetType}) where {RingType, RingElemType, MultSetType}
  return f.frac
end

# Automatic conversion
@Markdown.doc """
    (::LocalizedRing{RingType, RingElemType, MultSetType})(f::RingElemType) where {RingType, RingElemType, MultSetType}

Part of the minimal interface for localized rings. Suppose W = R[S‚Åª¬π]; then this routine 
returns the conversion of an element f ‚àà R to an element f//1 ‚àà W.
"""
(W::LocalizedRing{RingType, RingElemType, MultSetType})(f::RingElemType) where {RingType, RingElemType, MultSetType} = LocalizedRingElem((FractionField(original_ring(W)))(f), W)

@Markdown.doc """
    (W::LocalizedRing{RingType, RingElemType, MultSetType})(i::Int) where {RingType, RingElemType, MultSetType}

Part of the minimal interface for localized rings. This routine returns the conversion of 
an integer i to an element i//1 ‚àà W.
"""
(W::LocalizedRing{RingType, RingElemType, MultSetType})(i::Int) where {RingType, RingElemType, MultSetType} = W(original_ring(W)(i))

#################################################################
# Honest local rings arising from localizations at prime ideals #
#################################################################

@Markdown.doc """
    AbsComplementOfPrimeIdeal{RingType, RingElemType}

Abstract type for the multiplicatively closed sets S = R \\ P for prime ideals P in a commutative ring R. 
This is comprises both the complement of maximal ideals, as well as arbitrary prime ideals. 
In general, one expects different algorithmic backends for each one of the two cases. This will lead to a 
distinction of the associated concrete types.
"""
abstract type AbsComplementOfPrimeIdeal{RingType, RingElemType} <:AbsMultSet{RingType, RingElemType} end

@Markdown.doc """
    ComplementOfPrimeIdeal{RingType, RingElemType}

The multiplicatively closed set S given as the complement S = R ‚àñ P
of a prime ideal P in a commutative ring R of type `RingType` with elements 
of type `RingElemType`.
"""
mutable struct ComplementOfPrimeIdeal{RingType, RingElemType} <: AbsComplementOfPrimeIdeal{RingType, RingElemType}
  # essential fields
  P::Ideal{RingElemType}

  # fields for caching
  R::RingType # the base ring 

  function ComplementOfPrimeIdeal(P::Ideal{RingElemType}) where {RingElemType}
    S = new{parent_type(RingElemType), RingElemType}(P, base_ring(P))
    isprime(S.P) || error( "the given ideal is not prime" )
    return S
  end
end

function Base.in(f::RingElemType, S::ComplementOfPrimeIdeal{RingType, RingElemType}) where {RingType, RingElemType}
  return !(f in S.P)
end

@Markdown.doc """
    AbsLocalRing{RingType, RingElemType, MultSetType}

Abstract type for local rings arising from localizations at prime ideals.
"""
abstract type AbsLocalRing{RingType, RingElemType} <: AbsLocalizedRing{RingType, RingElemType, AbsComplementOfPrimeIdeal{RingType, RingElemType}} end

function original_ring(W::AbsLocalRing{RingType, RingElemType}) where {RingType, RingElemType}
  error("`original_ring` not implemented for local rings of type $(typeof(W))")
end

function inverted_set(W::AbsLocalRing{RingType, RingElemType}) where {RingType, RingElemType}
  error("`inverted_set` not implemented for local rings of type $(typeof(W))")
end

@Markdown.doc """
    AbsLocalRingElem{RingType, RingElemType} <: AbsLocalizedRingElem{RingType, RingElemType, AbsComplementOfPrimeIdeal{RingType, RingElemType}

Abstract type for elements of local rings arising from localizations at prime ideals.
"""
abstract type AbsLocalRingElem{RingType, RingElemType} <: AbsLocalizedRingElem{RingType, RingElemType, AbsComplementOfPrimeIdeal{RingType, RingElemType}} end

############################################################################
# Localizations of polynomial rings over admissible fields at prime ideals #
############################################################################
@Markdown.doc """
    mutable struct MPolyLocalRing{BaseRingType, RingType, RingElemType} <: AbsLocalRing{MPolyRing{BaseRingType}, MPolyElem{BaseRingType}}

The localization of a multivariate polynomial ring R = ùïú[x‚ÇÅ,‚Ä¶,x‚Çô] of type `RingType` over a base field ùïú of type `BaseRingType` and with elements of type `RingElemType` at a prime ideal P ‚äÇ R.
"""
mutable struct MPolyLocalRing{BaseRingType, RingType, RingElemType} <: AbsLocalRing{MPolyRing{BaseRingType}, MPolyElem{BaseRingType}}
  R::RingType # The parent ring which is being localized
  S::ComplementOfPrimeIdeal{RingType, RingElemType} 

  function MPolyLocalRing(R::RingType, 
      S::ComplementOfPrimeIdeal{RingType, RingElemType}
    ) where {BaseRingType, RingType<:MPolyRing{BaseRingType}, RingElemType}
    # TODO: Add some sanity checks here?
    R_loc = new{BaseRingType, RingType, RingElemType}(R,S)
    return R_loc
  end
end

function MPolyLocalRing( R::MPolyRing{BaseRingType}, P::Ideal{RingElemType} ) where {BaseRingType, RingElemType}
  return MPolyLocalRing(R, ComplementOfPrimeIdeal(P))
end

function original_ring(W::MPolyLocalRing{BaseRingType, RingType, RingElemType}) where {BaseRingType, RingType, RingElemType}
  return W.R
end

function inverted_set(W::MPolyLocalRing{BaseRingType, RingType, RingElemType}) where {BaseRingType, RingType, RingElemType}
  return W.S
end

mutable struct MPolyLocalRingElem{BaseRingType, RingType, RingElemType} <: AbsLocalRingElem{MPolyRing{BaseRingType}, MPolyElem{BaseRingType}} 
  frac::AbstractAlgebra.Generic.Frac{RingElemType}
  R_loc::MPolyLocalRing{BaseRingType, RingType, RingElemType}

  function MPolyLocalRingElem(f::AbstractAlgebra.Generic.Frac{RingElemType}, R_loc::MPolyLocalRing{BaseRingType, RingType, RingElemType}) where {BaseRingType, RingType, RingElemType}

    base_ring(parent(f)) == original_ring(R_loc) || error("the numerator and denominator of the given fraction do not belong to the original ring before localization")
    denominator(f) in inverted_set(R_loc) || error("the given denominator is not admissible for this localization")
    return new{BaseRingType, RingType, RingElemType}(f, R_loc)
  end
end

fraction(a::MPolyLocalRingElem{BaseRingType, RingType, RingElemType}) where {BaseRingType, RingType, RingElemType} = a.frac

parent(a::MPolyLocalRingElem{BaseRingType, RingType, RingElemType}) where {BaseRingType, RingType, RingElemType} = a.R_loc

(W::MPolyLocalRing{BaseRingType, RingType, RingElemType})(f::RingElemType) where {BaseRingType, RingType, RingElemType} = MPolyLocalRingElem((FractionField(original_ring(W)))(f), W)

(W::MPolyLocalRing{BaseRingType, RingType, RingElemType})(f::AbstractAlgebra.Generic.Frac{RingElemType}) where {BaseRingType, RingType, RingElemType} = MPolyLocalRingElem(f, W)
